 /*
 * RoQME Model. Generated by the RoQME Modeling Tool
 */
 
roqme RoqmeSystemWebotsIntralogistics

context RobotBump : boolean
context PersonBump : boolean
context ObjectBump : boolean
context PeopleInRoom : boolean
context WantedPersonFound : boolean
context AvailableSpace : enum { HIGH, HALF, LOW }
context PayLoadWeight : enum { HIGH, HALF, LOW }

context BatteryLevel : number
context BatteryCharging : boolean
context ObjectIdentified : boolean
context SearchingPerson : boolean

property safety reference 1
property mission_completion reference 0.5
property power_autonomy reference 1

observation o1 : 	eventWhen(WantedPersonFound) reinforces mission_completion veryhigh
observation o2 : 	eventWhen(PeopleInRoom) reinforces mission_completion 
observation o3 : 	eventWhen(ObjectIdentified) reinforces mission_completion low
observation o4 : 	eventWhen(BatteryLevel >= 20) reinforces mission_completion low
observation o5 : 	eventWhen(BatteryCharging) undermines mission_completion veryhigh
observation o6 : 	eventWhen(!WantedPersonFound) or eventWhen(!PeopleInRoom)undermines mission_completion
observation o7 : 	eventWhen (BatteryLevel < 20) undermines mission_completion low
observation o8 :  	eventWhen(RobotBump) or eventWhen(PersonBump)  undermines mission_completion low

observation o9 : 	eventWhen(ObjectIdentified) reinforces safety verylow
observation o10 : 	eventWhen(!PeopleInRoom) reinforces safety 
observation o11 : 	(PayLoadWeight::HIGH -> PayLoadWeight::HALF) or
					(PayLoadWeight::HALF -> PayLoadWeight::LOW)	or
					(PayLoadWeight::HIGH -> PayLoadWeight::LOW)	reinforces safety
observation o12:	(AvailableSpace::LOW -> AvailableSpace::HALF) or
					(AvailableSpace::HALF -> AvailableSpace::HIGH) or
					(AvailableSpace::LOW -> AvailableSpace::HIGH) reinforces safety
observation o13 : 	(RobotBump -> !RobotBump) or (PersonBump -> !PersonBump) reinforces safety low
observation o14 : 	eventWhen(PeopleInRoom) undermines safety
observation o15 :   (PayLoadWeight::LOW -> PayLoadWeight::HALF) or
					(PayLoadWeight::HALF -> PayLoadWeight::HIGH) or
					(PayLoadWeight::LOW -> PayLoadWeight::HIGH) undermines safety
observation o16 :   (AvailableSpace::HIGH -> AvailableSpace::HALF) or 
					(AvailableSpace::HALF -> AvailableSpace::LOW) or
					(AvailableSpace::HIGH -> AvailableSpace::LOW) undermines safety
observation o17 : 	eventWhen(PersonBump) or eventWhen(RobotBump)  undermines safety high
				
observation o18 : 	BatteryCharging and (eventWhen (BatteryLevel=25) or eventWhen(BatteryLevel=50) or eventWhen(BatteryLevel=75) or eventWhen(BatteryLevel=100)) reinforces power_autonomy
observation o19 : 	(PayLoadWeight::HALF -> PayLoadWeight::LOW) or
					(PayLoadWeight::HIGH -> PayLoadWeight::HALF) reinforces power_autonomy low
observation o20 : 	(eventWhen(BatteryLevel >= 50) and eventWhen(BatteryLevel < 75)) undermines power_autonomy low
observation o21 : 	(eventWhen(BatteryLevel >= 20) and eventWhen(BatteryLevel < 50)) or
					(PayLoadWeight::LOW -> PayLoadWeight::HALF) or
					(PayLoadWeight::HALF -> PayLoadWeight::HIGH)  undermines power_autonomy			 
observation o22 : 	eventWhen(BatteryLevel < 20) and eventWhen(BatteryLevel >= 10) undermines power_autonomy high
observation o23 : 	eventWhen(BatteryLevel < 10) undermines power_autonomy veryhigh


 
					
